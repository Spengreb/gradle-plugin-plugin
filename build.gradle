version = 0.1
group = 'com.iadams.gradle.plugins'

apply plugin: 'groovy'
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/integTest.gradle"
apply from: "$rootDir/gradle/publishing.gradle"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
	jcenter()
	maven {
		url "https://plugins.gradle.org/m2/"
	}
}
dependencies {
	compile localGroovy()
	compile gradleApi()
	compile "org.sonarqube.gradle:gradle-sonarqube-plugin:1.0"

	testCompile gradleTestKit()
	testCompile 'org.spockframework:spock-core:1.0-groovy-2.4', {
		exclude module: 'groovy-all'
	}
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
	def outputDir = file("$buildDir/$name")

	inputs.files sourceSets.main.runtimeClasspath
	outputs.dir outputDir

	doLast {
		outputDir.mkdirs()
		file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
	}
}

// Add the classpath file to the test runtime classpath
dependencies {
	testRuntime files(createClasspathManifest)
}
